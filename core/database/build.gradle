 
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'

android {
    namespace 'com.buzbuz.smartautoclicker.core.database'
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        targetSdk rootProject.ext.compileSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    compileOptions {
        kotlin {
            kotlinOptions {
                freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
            }
        }

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.includeAndroidResources = true
    }

    sourceSets {
        // Adds exported schema location as test app assets.
        getByName("test").assets.srcDirs += "$projectDir/schemas".toString()
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${rootProject.ext.kotlinSerializationVersion}"

    implementation "androidx.lifecycle:lifecycle-livedata-ktx:${rootProject.ext.androidxLifecycleVersion}"
    implementation "androidx.room:room-ktx:${rootProject.ext.androidxRoomVersion}"
    kapt "androidx.room:room-compiler:${rootProject.ext.androidxRoomVersion}"

    implementation project(path: ':core:extensions')

    testImplementation "junit:junit:${rootProject.ext.jUnitVersion}"
    testImplementation "androidx.arch.core:core-testing:${rootProject.ext.androidxArchCoreTestingVersion}"
    testImplementation "androidx.room:room-testing:${rootProject.ext.androidxRoomVersion}"
    testImplementation "androidx.test:core:${rootProject.ext.androidxTestCoreVersion}"
    testImplementation "androidx.test.ext:junit:${rootProject.ext.androidxTestExtVersion}"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinx_version"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoCoreVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.ext.robolectricVersion}"

    // The following dependency is required to compile using JDK as Room needs javax.annotation.Generated
    compileOnly "javax.annotation:javax.annotation-api:1.3.2"
}